      PROGRAM RECOMB(INPUT,OUTPUT,TAPE5=INPUT,TAPE6=OUTPUT)
      INTEGER T,MASKS(60),GAMETE(1000),RALLELE(1000),NCOUNT(1024),
     1GAM2(1000),R2(1000)
      REAL FITNESS(60),CUMMUT(1000),CUMFIT(1000)
      NLOCI=20
      NTENS=(NLOCI+9)/10
      MULT=5**19
      RANL=2.**(-48)
      READ(5,1) NRAND
    1 FORMAT(O20)
      WRITE(6,2) NRAND
    2 FORMAT(1H1,24HINITIAL RANDOM NUMBER = ,O20,2X,5HOCTAL)
 1000 READ(5,3) FREQR,NPOP,U,S,T
    3 FORMAT(F10.8,I5,E10.2,F10.8,I5)
      IF(NPOP.EQ.0) STOP 1
      WRITE(6,4) FREQR,NPOP,U,S,T
    4 FORMAT(1H1,43HINITIAL FREQUENCY OF RECOMBINATOR ALLELE = ,G15.8//
     1 1X,18HPOPULATION SIZE = ,I5//1X,27HMUTATION RATE PER GAMETE = ,
     2 G15.8//1X,33HSELECTIVE ADVANTAGE OF MUTANTS = ,G15.8//1X,4HRUN ,
     3 I5,1X,11HGENERATIONS/)
      MASKS(1)=1
      DO 5 I=2,NLOCI
    5 MASKS(I)=2*MASKS(I-1)
      DO 6 I=1,NPOP
      GAMETE(I)=0
    6 RALLELE(I)=0
      NN=FLOAT(NPOP)*FREQR+0.5
      DO 7 I=1,NN
    7 RALLELE(I)=1
      DO 9 I=1,1024
      II=I-1
      J=0
      DO 8 K=1,NLOCI
      KK=II.AND.MASKS(K)
      IF(KK.NE.0) J=J+1
    8 CONTINUE
    9 NCOUNT(I)=J
      FITNESS(1)=1.
      DO 10 I=2,NLOCI
   10 FITNESS(I)=FITNESS(I-1)*(1.+S)
      CUMMUT(1)=0.
      ULOG=ALOG(U)
      CURLOG=-U
      IF(CURLOG.GT.-50.) CUMMUT(1)=EXP(CURLOG)
      DO 11 I=1,NPOP
      CURLOG=CURLOG+ULOG-ALOG(FLOAT(I))
      CUMMUT(I+1)=CUMMUT(I)
      IF(CURLOG.GT.-50.) CUMMUT(I+1)=CUMMUT(I+1)+EXP(CURLOG)
   11 CONTINUE
      WRITE(6,115)
  115 FORMAT(1H0,1X,4HTIME,1X,5HFIXED,3X,6HEVMEAN,5X,5HAVFIT,4X,
     1 6HAVRFIT,4X,6HAVPFIT,5X,5HRATIO,4X,6HFREQ R/)
C  LOOP OVER GENERATIONS, 1 TO T
      NFIXED=0
      DO 30 ITIME=1,T
C  COUNT MUTANTS. GET CUMULATIVE FITNESSES, MEAN FITNESS OF R, RPLUS
      SUMFITR=0.
      NUMR=0
      CUMF=0.
      NSUMEV=0
      DO 13 I=1,NPOP
      IC=0
      M=GAMETE(I)
      DO 12 J=1,NTENS
      K=MOD(M,1024)
      IC=IC+NCOUNT(K+1)
   12 M=M/1024
      NSUMEV=NSUMEV+IC
      CURFIT=FITNESS(IC+1)
      CUMF=CUMF+CURFIT
      CUMFIT(I)=CUMF
      IF(RALLELE(I).NE.1) GO TO 13
      NUMR=NUMR+1
      SUMFITR=SUMFITR+CURFIT
   13 CONTINUE
      EMEAN=FLOAT(NSUMEV)/FLOAT(NPOP)
      AVFIT=CUMFIT(NPOP)/FLOAT(NPOP)
      AVRFIT=SUMFITR/FLOAT(NUMR)
      FRQR=FLOAT(NUMR)/FLOAT(NPOP)
      IF(FRQR.EQ.1..OR.FRQR.EQ.0.) GO TO 40
      AVPFIT=(AVFIT-FRQR*AVRFIT)/(1.-FRQR)
C  MATING AND MEIOSIS
      DO 15 IN=1,NPOP
C  PICK TWO PARENTS
      NRAND=NRAND*MULT
      RAND=FLOAT(NRAND)*RANL
      X=CUMF*RAND
      IPOSIT=FLOAT(NPOP)*RAND
      IF(CUMFIT(IPOSIT).GE.X) GO TO 132
  131 IPOSIT=IPOSIT+1
      IF(CUMFIT(IPOSIT).LT.X) GO TO 131
      GO TO 134
  132 IPOSIT=IPOSIT-1
      IF(IPOSIT.EQ.0) GO TO 133
      IF(CUMFIT(IPOSIT).GE.X) GO TO 132
  133 IPOSIT=IPOSIT+1
  134 IONE=IPOSIT
      NRAND=NRAND*MULT
      RAND=FLOAT(NRAND)*RANL
      X=CUMF*RAND
      IPOSIT=FLOAT(NPOP)*RAND
      IF(CUMFIT(IPOSIT).GE.X) GO TO 136
  135 IPOSIT=IPOSIT+1
      IF(CUMFIT(IPOSIT).LT.X) GO TO 135
      GO TO 138
  136 IPOSIT=IPOSIT-1
      IF(IPOSIT.EQ.0) GO TO 137
      IF(CUMFIT(IPOSIT).GE.X) GO TO 136
  137 IPOSIT=IPOSIT+1
  138 ITWO=IPOSIT
      IGAM=GAMETE(IONE)
      IF(RALLELE(IONE).EQ.0.OR.RALLELE(ITWO).EQ.0) GO TO 14
      NRAND=NRAND*MULT
      MASKR=NRAND/1048576
      IGAM=IGAM.AND.MASKR.OR.GAMETE(ITWO).AND.(.NOT.MASKR)
   14 GAM2(IN)=IGAM
      R2(IN)=RALLELE(IONE)
   15 CONTINUE
      EVMEAN=EMEAN+FLOAT(NFIXED)
      RATIO=AVRFIT/AVPFIT
      WRITE(6,16) ITIME,NFIXED,EVMEAN,AVFIT,AVRFIT,AVPFIT,RATIO,FRQR
   16 FORMAT(1X,2I5,6F10.5)
C  MASKING OUT FIXED LOCI
      MFIXED=2**NLOCI-1
      MLOST=2**NLOCI-1
      DO 17 I=1,NPOP
      MFIXED=MFIXED.AND.GAM2(I)
   17 MLOST=MLOST.AND.(.NOT.GAM2(I))
      MLOST=MFIXED.OR.MLOST
      NMASK=.NOT.MFIXED
      DO 175 I=1,NPOP
      GAMETE(I)=GAM2(I).AND.NMASK
  175 RALLELE(I)=R2(I)
C  COUNT NO. OF NONSEGREGATING LOCI
      NOTSEG=0
      ML=MLOST
      DO 18 I=1,NTENS
      M=MOD(ML,1024)
      NOTSEG=NOTSEG+NCOUNT(M+1)
   18 ML=ML/1024
      IF(NOTSEG.EQ.0) STOP 2
      DO 181 I=1,NLOCI
      M=MFIXED.AND.MASKS(I)
      IF(M.NE.0) NFIXED=NFIXED+1
  181 CONTINUE
C  MUTATION - HOW MANY NEW MUTANTS
      NRAND=NRAND*MULT
      RAND=FLOAT(NRAND)*RANL
      DO 19 I=1,NPOP
      IF(RAND.LE.CUMMUT(I)) GO TO 20
   19 CONTINUE
   20 NMUTS=I
      IF(NMUTS.EQ.0) GO TO 30
      DO 23 I=1,NMUTS
      NRAND=NRAND*MULT
      RAND=FLOAT(NRAND)*RANL
      II=FLOAT(NOTSEG)*RAND+1.0
      NRAND=NRAND*MULT
      RAND=FLOAT(NRAND)*RANL
      JJ=FLOAT(NPOP)*RAND+1.0
      KK=0
      DO 21 J=1,NLOCI
      LL=MLOST.AND.MASKS(J)
      IF(LL.EQ.0) KK=KK+1
      IF(KK.EQ.II) GO TO 22
   21 CONTINUE
   22 GAMETE(JJ)=GAMETE(JJ).OR.LL
   23 CONTINUE
   30 CONTINUE
      WRITE(6,31) NRAND
   31 FORMAT(1H0,21HLAST RANDOM NUMBER = ,O20,2X,5HOCTAL)
      GO TO 1000
   40 WRITE(6,41) ITIME,NRAND
   41 FORMAT(1H0,20HFIXATION, GENERATION, I5/1X,21HLAST RANDOM NUMBER =
     1 ,O20,2X,5HOCTAL)
      GO TO 1000
      END

00000143661472465631
0.50000000  100  1.00E+021.00000000  100
0.50000000  100  1.00E+021.00000000  100
0.50000000  100  1.00E+021.00000000  100
0.50000000  100  1.00E+021.00000000  100
0.50000000  100  1.00E+021.00000000  100

